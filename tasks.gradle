import groovy.json.JsonOutput
import groovy.json.JsonSlurper

def testContainerPostgresVersion = '12.2'

def findJar(Object... prefixes) {
    configurations.runtimeClasspath.filter { file ->
        prefixes.any { prefix -> file.name.startsWith(prefix)
        }
    }
}

task copyToLib(type: Copy, dependsOn: [compileJava]) {
    from findJar('h2', 'liquibase', 'logback-classic', 'logback-core', 'slf4j-api', 'snakeyaml')
    into "${project.buildDir}/libs/liquibase"
}

tasks.compileJava.finalizedBy(tasks.copyToLib)
// disable the test task when running the alert server to speed up startup time.
gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(runServer)) {
        compileTestJava.enabled = false
        test.enabled = false
    }
}

task createKeystore(type: com.synopsys.integration.alert.build.CreateKeystoreTask) {
    doFirst {
        mkdir "${project.buildDir}/certs"
    }
}

task createTruststore(type: Copy) {
    from "${System.getProperty('java.home')}/lib/security/"
    include 'cacerts'
    into "${project.buildDir}/certs/"
    rename 'cacerts', 'blackduck-alert.truststore'
}

task runServer(type: com.synopsys.integration.alert.build.RunServerTask, dependsOn: [build, createKeystore, createTruststore]) {
    postgresVersion = testContainerPostgresVersion
}

tasks.createKeystore.onlyIf { !file("${project.buildDir}/certs/blackduck-alert.keystore").exists() }
tasks.createTruststore.onlyIf { !file("${project.buildDir}/certs/blackduck-alert.truststore").exists() }
tasks.runServer.mustRunAfter(createKeystore)
tasks.runServer.mustRunAfter(createTruststore)

// Build tasks

task cleanBundles(type: Delete) {
    File testDatabaseDirectory = new File('testDB')
    if (testDatabaseDirectory.exists()) {
        println "Deleting ${testDatabaseDirectory.canonicalPath}"
        testDatabaseDirectory.deleteDir()
    }

    File staticResourcesDirectory = new File('src/main/static')
    if (staticResourcesDirectory.exists()) {
        println "Deleting ${staticResourcesDirectory.canonicalPath}"
        staticResourcesDirectory.deleteDir()
    }

    File emailFileDirectory = new File('email')
    if (emailFileDirectory.exists()) {
        println "Deleting ${emailFileDirectory.canonicalPath}"
        emailFileDirectory.deleteDir()
    }
}

tasks.clean.finalizedBy(cleanBundles)

task helmLint(type: Exec) {
    workingDir = "${projectDir}/deployment/helm/synopsys-alert/"
    commandLine "helm", "lint", "${projectDir}/deployment/helm/synopsys-alert/"
}

task helmDryRun(type: Exec) {
    workingDir = "${projectDir}/deployment/helm/synopsys-alert/"
    commandLine "helm", "install", "alert", "${projectDir}/deployment/helm/synopsys-alert/", "-n", "alert", "--dry-run"
}

task cleanHelmTmp(type: Delete) {
    // synopsys-alert@tmp is created by Jenkins when we switch directories. Once we have updated the builds to use the gradle tasks we dont need this cleanup anymore
    File helmTmpDirectory = new File("${projectDir}/deployment/helm/synopsys-alert@tmp/")
    if (helmTmpDirectory.exists()) {
        println "Deleting ${helmTmpDirectory.canonicalPath}"
        helmTmpDirectory.deleteDir()
    }
}

tasks.clean.finalizedBy(cleanHelmTmp)

task helmValidation(dependsOn: [helmLint, helmDryRun, cleanHelmTmp]) {
    helmDryRun.mustRunAfter helmLint
    cleanHelmTmp.mustRunAfter helmDryRun
}

task updateNpmVersion(type: Task) {
    final def packageJsonFile = new File("${projectDir}/package.json")
    def packageSlurper = new JsonSlurper()
    def packageJson = packageSlurper.parse file("${projectDir}/package.json")
    if (packageJson.version != version) {
        packageJson.version = version

        final def updatedPackageJson = JsonOutput.toJson(packageJson)

        packageJsonFile.delete()
        packageJsonFile << JsonOutput.prettyPrint(updatedPackageJson)
    }
}

npm_run() {
    args = ['build']
}

tasks.npm_run.mustRunAfter(updateNpmVersion)

task webpack(type: Exec) {
    inputs.file("package-lock.json")
    inputs.file("webpack.config.js")
    inputs.dir("$projectDir/src/main")

    commandLine "$projectDir/node_modules/.bin/webpack"
}

task createAboutText(type: Task) {
    final def aboutFile = new File("${projectDir}/src/main/resources/about.txt")
    aboutFile.delete()
    def readmeContentArray = new File("${projectDir}/README.md").text.readLines()
    def descriptionStart = readmeContentArray.indexOf("<!-- description-text-start -->") + 1
    def descriptionEnd = readmeContentArray.indexOf("<!-- description-text-end -->")
    def description = readmeContentArray.subList(descriptionStart, descriptionEnd).join(" ")

    def projectUrlStart = readmeContentArray.indexOf("<!-- project-url-text-start -->") + 1
    def projectUrlEnd = readmeContentArray.indexOf("<!-- project-url-text-end -->")
    def projectUrlText = readmeContentArray.subList(projectUrlStart, projectUrlEnd).join(" ")
    def gitUrl = projectUrlText.substring(projectUrlText.indexOf("https"))
    gitUrl = gitUrl.substring(0, gitUrl.indexOf("releases"))

    def time = new Date().format('yyyy-MM-dd\'T\'HH:mm:ss.SSSSSS\'Z\'');
    final def aboutJson = JsonOutput.toJson([version: version, projectUrl: gitUrl, description: description, created: time])
    logger.info("About text file: {} content: {}", aboutFile, aboutJson)
    aboutFile << aboutJson
}

task copyToTemplates(type: Copy, dependsOn: [npm_run]) {
    from "${project.buildDir}/resources/main/static/index.html"
    into "${project.buildDir}/resources/main/templates/"
}

tasks.compileJava.mustRunAfter(createAboutText)
tasks.compileJava.finalizedBy(copyToTemplates)


